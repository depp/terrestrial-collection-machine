load("//tools:copts.bzl", "COPTS", "CXXOPTS")

cc_binary(
    name = "tcm_release",
    srcs = [
        "gl.h",
        "main_release.c",
        ":packed_shaders",
    ],
    copts = COPTS,
    linkopts = [
        "-framework OpenGL",
    ],
    deps = [
        "@glfw3",
    ],
)

cc_binary(
    name = "tcm_dev",
    srcs = [
        "gl.h",
        "loader.cpp",
        "loader.hpp",
        "log.cpp",
        "log.hpp",
        "main_dev.cpp",
        "shader.cpp",
        "shader.hpp",
    ],
    copts = CXXOPTS,
    linkopts = [
        "-framework OpenGL",
    ],
    deps = [
        "@glfw3",
    ],
)

py_binary(
    name = "pack_shaders",
    srcs = ["pack_shaders.py"],
    # PY3 is default, but this suppresses the warning about Python 3 if the
    # build rule fails.
    python_version = "PY3",
)

genrule(
    name = "packed_shaders",
    srcs = glob([
        "shader/*.vert",
        "shader/*.frag",
    ]),
    outs = [
        "packed_shaders.h",
        "packed_shaders.c",
    ],
    cmd = "./$(location :pack_shaders) " +
          "--out-c=$(location packed_shaders.c) " +
          "--out-h=$(location packed_shaders.h) " +
          "$(SRCS)",
    tools = [":pack_shaders"],
)
